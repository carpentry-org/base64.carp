(use-all Array Char Int String)

(defmodule Base64

  (doc mime-charset "The Base64 character set as specified by MIME.")
  (def mime-charset [
    \A \B \C \D \E \F \G \H \I \J \K \L \M \N \O \P \Q
    \R \S \T \U \V \W \X \Y \Z \a \b \c \d \e \f \g \h
    \i \j \k \l \m \n \o \p \q \r \s \t \u \v \w \x \y
    \z \0 \1 \2 \3 \4 \5 \6 \7 \8 \9 \+ \/
  ])

  (doc mime-padding "The Base64 padding as specified by MIME.")
  (def mime-padding \=)

  (hidden from-int-ref)
  (defn from-int-ref [x] (from-int @x))

  (hidden to-int-ref)
  (defn to-int-ref [x] (to-int @x))

  (doc decode-using "Decodes a base64-encoded datum using a user-supplied character set.")
  (sig decode-using (λ [&(Array Char) Char &String] (Array Int)))
  (defn decode-using [charset padding str]
      (let-do [bytes (chars str)
            decoded []]
        (while (/= (length &bytes) 0)
          (let-do [b0 (index-of charset @(nth &bytes 0))
                   b1 (index-of charset @(nth &bytes 1))
                   third @(nth &bytes 2)
                   b2 (index-of charset third)
                   fourth @(nth &bytes 3)
                   b3 (index-of charset fourth)]
            (set! bytes (suffix-array &bytes 4))
            (set! decoded (Array.push-back decoded (bit-or (bit-shift-left (bit-and b0 63) 2) (bit-shift-right b1 4))))
            (when (/= third padding)
              (set! decoded (Array.push-back decoded (bit-or (bit-shift-left (bit-and b1 15) 4) (bit-shift-right b2 2)))))
            (when (/= fourth padding)
              (set! decoded (Array.push-back decoded (bit-or (bit-shift-left (bit-and b2 3) 6) b3))))))
        decoded))

  (doc decode "Decodes a base64-encoded datum using mime-charset.")
  (defn decode [str]
    (decode-using &mime-charset mime-padding str))

  (doc decode-str "Decodes a base64-encoded string using mime-charset.")
  (defn decode-str [str]
    (from-chars &(copy-map from-int-ref
                           &(decode-using &mime-charset mime-padding str))))

  (doc encode-using "Encodes a base64-encoded datum using a user-supplied character set.")
  (sig encode-using (λ [&(Array Char) Char &(Array Int)] String))
  (defn encode-using [charset pad s]
    (let-do [l (length s)
             idx 0
             enc @""]
      (while (> l idx)
        (let-do [b0 @(nth s idx)]
        (case (- l idx)
          1
            (let-do [c0 (nth charset (bit-shift-right b0 2))
                     c1 (nth charset (bit-shift-left (bit-and b0 3) 4))]
              (set! enc (str* enc @c0 @c1 pad pad))
              (set! idx l))
          2
            (let-do [b1 @(nth s (inc idx))
                     c0 (nth charset (bit-shift-right b0 2))
                     c1 (nth charset (bit-or (bit-shift-left (bit-and b0 3) 4)
                                             (bit-shift-right b1 4)))
                     c2 (nth charset (bit-shift-left (bit-and b1 15) 2))
                     ]
              (set! enc (str* enc @c0 @c1 @c2 pad))
              (set! idx l))
          (let-do [b1 @(nth s (inc idx))
                   b2 @(nth s (+ idx 2))
                   c0 (nth charset (bit-shift-right b0 2))
                   c1 (nth charset (bit-or (bit-shift-left (bit-and b0 3) 4)
                                           (bit-shift-right b1 4)))
                   c2 (nth charset (bit-or (bit-shift-left (bit-and b1 15) 2)
                                           (bit-shift-right b2 6)))
                   c3 (nth charset (bit-and b2 63))
                   ]
            (set! enc (str* enc @c0 @c1 @c2 @c3))
            (set! idx (+ idx 3))))))
        enc))

  (doc encode "Encodes a datum into base64 using mime-charset.")
  (defn encode [s]
    (encode-using &mime-charset mime-padding s))

  (doc encode-str "Encodes a string into base64 using mime-charset.")
  (defn encode-str [s]
    (encode-using &mime-charset mime-padding &(copy-map to-int-ref &(chars s))))
)
